<!-- landscape mesh -->
<script type="application/x-glsl" id="sky-vertex">
varying vec2 vUV;
varying float vDensity;
varying float vDiff;
uniform float fisheye;
uniform float aspect;

uniform float space;

const float fogScale = 12.0;
const float fogOffset = -0.5;

void main() {
  vUV = uv;

  // Get world pos
  vec4 pos = vec4(position, 1.0);
  vec3 worldPos = (modelMatrix * pos).xyz;
  vec3 capPos = worldPos;
  capPos.y = max(8.0, capPos.y);

  // Volumetric fog per vertex
  vec3 diff = cameraPosition - capPos;
  float factor = min(10.0, length(diff) / abs(diff.y));
  vec2 depths = (-vec2(capPos.y, cameraPosition.y) + fogOffset) * fogScale;
  vDensity = abs(exp(depths.x) - exp(depths.y)) * factor * .001;

  // Brightness decay at horizon
  float scale = (cameraPosition.y + 1.0) * 60.0;
  vDiff = (worldPos.y + scale) * .0033 * scale / (scale + 1.0);

  // Project
  gl_Position = projectionMatrix * modelViewMatrix * pos;

  if (space > 0.5) {
    vDiff = 1.0;
    vDensity = 0.0;
  }

  // fisheye
  if (gl_Position.z / gl_Position.w > 0.01) {
    vec2 dxy = gl_Position.xy / gl_Position.w * vec2(aspect, 1.0);
    gl_Position.xy /= min(2.3, 1.0 + dot(dxy, dxy) * fisheye * .1);
  }
}
</script>

<!-- sky + volumetric fog at infinity -->
<script type="application/x-glsl" id="sky-density">
uniform sampler2D texture;
varying vec2 vUV;
varying float vDensity;
varying float vDiff;

void main() {
  vec4 sample = texture2D(texture, vUV);

  float diff = clamp(vDiff, 0.0, 1.0);
  float decay = (3.0 - 2.0*diff)*diff*diff;

  gl_FragColor = vec4(sample.xyz * decay + vDensity, sample.w);
}
</script>

